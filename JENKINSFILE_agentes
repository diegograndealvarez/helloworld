// Reto 2 – Distribució d'agents
// Pipeline amb execució distribuïda i paral·lela
// Evolució del Jenkinsfile del CP1.1

pipeline {
  // No definim agent global: cada etapa escull el seu
  agent none

  options {
    timestamps()
    skipDefaultCheckout(true)
  }

  stages {

    stage('Get Code (master)') {
      // El checkout es fa al node principal
      // El codi es comparteix amb la resta d’agents mitjançant stash
      agent { label 'master' }
      steps {
        cleanWs()
        sh '''
          whoami
          hostname
          echo ${WORKSPACE}
        '''
        checkout scm
        stash name: 'src', includes: '**/*'
      }
    }

    stage('Tests en paral·lel') {
      // Execució paral·lela de totes les proves independents
      parallel {

        stage('Unit') {
          // Proves unitàries en un agent dedicat
          agent { label 'agent-unit' }
          steps {
            cleanWs()
            unstash 'src'
            sh '''
              whoami
              hostname
              echo ${WORKSPACE}
              export PYTHONPATH=$WORKSPACE
              pip3 install pytest flask flake8 bandit coverage
              mkdir -p reports/unit
              pytest test/unit --junitxml=reports/unit/unit-tests.xml
            '''
          }
          post {
            always {
              junit allowEmptyResults: true, testResults: 'reports/unit/unit-tests.xml'
              stash name: 'unit-reports', includes: 'reports/unit/**', allowEmpty: true
              cleanWs()
            }
          }
        }

        stage('Rest') {
          // Proves REST executades en un agent separat
          agent { label 'agent-rest' }
          steps {
            cleanWs()
            unstash 'src'
            sh '''
              whoami
              hostname
              echo ${WORKSPACE}
              export PYTHONPATH=$WORKSPACE
              pytest test/rest || true
            '''
          }
          post {
            always {
              cleanWs()
            }
          }
        }

        stage('Static') {
          // Anàlisi estàtica de codi amb flake8
          agent { label 'agent-unit' }
          steps {
            cleanWs()
            unstash 'src'
            sh '''
              whoami
              hostname
              echo ${WORKSPACE}
              export PYTHONPATH=$WORKSPACE
              mkdir -p reports/static
              python3 -m flake8 app > reports/static/flake8-report.txt || true
            '''
            recordIssues(
              tools: [flake8(pattern: 'reports/static/flake8-report.txt')],
              qualityGates: [
                [threshold: 8, type: 'TOTAL', unstable: true],
                [threshold: 10, type: 'TOTAL', unstable: false]
              ]
            )
          }
          post {
            always {
              stash name: 'static-reports', includes: 'reports/static/**', allowEmpty: true
              cleanWs()
            }
          }
        }

        stage('Security') {
          // Anàlisi de seguretat amb Bandit
          agent { label 'agent-unit' }
          steps {
            cleanWs()
            unstash 'src'
            sh '''
              whoami
              hostname
              echo ${WORKSPACE}
              export PYTHONPATH=$WORKSPACE
              mkdir -p reports/security
              python3 -m bandit --exit-zero -r app \
                -f custom \
                -o reports/security/bandit.out \
                --msg-template "{abspath}:{line}: [{test_id}] {msg}"
            '''
            recordIssues(
              tools: [pyLint(name: 'Bandit', pattern: 'reports/security/bandit.out')],
              qualityGates: [
                [threshold: 2, type: 'TOTAL', unstable: true],
                [threshold: 4, type: 'TOTAL', unstable: false]
              ]
            )
          }
          post {
            always {
              stash name: 'security-reports', includes: 'reports/security/**', allowEmpty: true
              cleanWs()
            }
          }
        }

      }
    }

    stage('Coverage') {
      // Generació de cobertura (executada fora del paral·lel)
      // Es deixa seqüencial per evitar conflictes sobre el mateix conjunt de tests
      agent { label 'agent-unit' }
      steps {
        cleanWs()
        unstash 'src'
        sh '''
          whoami
          hostname
          echo ${WORKSPACE}
          export PYTHONPATH=$WORKSPACE
          python3 -m coverage run --branch --source=app -m pytest test/unit
          python3 -m coverage xml -o coverage.xml
        '''
        script {
          recordCoverage(
            tools: [[parser: 'COBERTURA', pattern: 'coverage.xml']]
          )
        }
        archiveArtifacts artifacts: 'coverage.xml', fingerprint: true
      }
      post {
        always {
          cleanWs()
        }
      }
    }

    stage('Performance') {
      // Proves de rendiment amb JMeter
      // S’executen en un agent REST dedicat
      agent { label 'agent-rest' }
      steps {
        cleanWs()
        unstash 'src'
        sh '''
          whoami
          hostname
          echo ${WORKSPACE}
          set -e
          export PYTHONPATH=$WORKSPACE
          mkdir -p reports/performance
          export FLASK_APP=app/api.py
          nohup flask run --host=127.0.0.1 --port=5000 > reports/performance/flask.log 2>&1 &
          FLASK_PID=$!
          for i in $(seq 1 20); do
            if curl -s http://127.0.0.1:5000/calc/add/1/2 >/dev/null; then
              echo "Flask OK"
              break
            fi
            sleep 0.5
          done
          jmeter -n -t test/jmeter/flask.jmx -l reports/performance/results.jtl
          kill $FLASK_PID || true
        '''
      }
      post {
        always {
          perfReport sourceDataFiles: 'reports/performance/results.jtl'
          cleanWs()
        }
      }
    }

  }
}

